buildscript {
    ext.kotlin_version = '1.1.51'
    repositories {
        google()
        jcenter()
        maven { url 'https://maven.fabric.io/public' }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.0.0-beta7'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'io.fabric.tools:gradle:1.24.2'
    }
}

allprojects {
    repositories {
        google()
        jcenter()
        mavenLocal()
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

apply from: 'dependencies.gradle'

subprojects {
    apply plugin: 'checkstyle'

    afterEvaluate {project ->
        check.dependsOn 'checkstyle'
        task checkstyle(type: Checkstyle) {
            configFile file('config/checkstyle/checkstyle.xml')
            source 'src'
            classpath = files()
        }
        checkstyle {
            ignoreFailures = false
            toolVersion = "6.17"
        }
        if (project.hasProperty("android")) {
            android {
                compileSdkVersion vers.compileSdkVersion
                buildToolsVersion vers.buildToolsVersion

                defaultConfig {
                    minSdkVersion vers.minSdkVersion
                    targetSdkVersion vers.targetSdkVersion
                }
                lintOptions {
                    abortOnError true
                    disable 'UnusedResources' // https://issuetracker.google.com/issues/63150366
                    disable 'InvalidPackage'
                }
                dexOptions {
                    dexInProcess = true
                }
            }
        }
    }
}

configurations {
    ktlint
}

dependencies {
    ktlint 'com.github.shyiko:ktlint:0.9.0'
}

task lintKotlin(type: JavaExec) {
    main = "com.github.shyiko.ktlint.Main"
    classpath = configurations.ktlint
    args "*/src/**/*.kt"
}
