buildscript {
    ext.kotlin_version = '1.2.0'
    repositories {
        google()
        jcenter()
        maven { url 'https://maven.fabric.io/public' }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.0.1'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'io.fabric.tools:gradle:1.25.1'
    }
}

plugins {
    id 'com.github.ben-manes.versions' version '0.17.0'
}

allprojects {
    repositories {
        google()
        jcenter()
        mavenLocal()
        mavenCentral()
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

apply from: 'dependencies.gradle'

subprojects {
    apply plugin: 'checkstyle'

    afterEvaluate {project ->
        check.dependsOn 'checkstyle'
        task checkstyle(type: Checkstyle) {
            configFile file('config/checkstyle/checkstyle.xml')
            source 'src'
            classpath = files()
        }
        checkstyle {
            ignoreFailures = false
            toolVersion = "6.17"
        }
        if (project.hasProperty("android")) {
            android {
                compileSdkVersion vers.compileSdkVersion
                buildToolsVersion vers.buildToolsVersion
                defaultConfig {
                    minSdkVersion vers.minSdkVersion
                    targetSdkVersion vers.targetSdkVersion
                    multiDexEnabled true
                }
                lintOptions {
                    abortOnError true
                    disable 'UnusedResources' // https://issuetracker.google.com/issues/63150366
                    disable 'InvalidPackage'
                }
                dexOptions {
                    dexInProcess = true
                }
                compileOptions {
                    sourceCompatibility JavaVersion.VERSION_1_8
                    targetCompatibility JavaVersion.VERSION_1_8
                }
            }
        }
        kotlin {
            experimental {
                coroutines "enable"
            }
        }
    }
}

configurations {
    ktlint
}

dependencies {
    ktlint 'com.github.shyiko:ktlint:0.14.0'
}

task lintKotlin(type: JavaExec) {
    main = "com.github.shyiko.ktlint.Main"
    classpath = configurations.ktlint
    args "*/src/**/*.kt"
}
